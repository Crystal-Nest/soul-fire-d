plugins {
  id "multiloader-loader"
  id "fabric-loom"
}

dependencies {
  minecraft "com.mojang:minecraft:$minecraft_version"
  mappings loom.layered {
    officialMojangMappings()
    parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
  }
  modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
  modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
  modImplementation "it.crystalnest:cobweb-$name:$minecraft_version-$cobweb_version"
  modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:$fcap_version"
}

loom {
  def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
  if (aw.exists()) {
    accessWidenerPath.set(aw)
  }
  mixin {
    defaultRefmapName.set("${mod_id}.refmap.json")
  }
  runs {
    client {
      client()
      setConfigName("Fabric Client")
      ideConfigGenerated(true)
      runDir("run/client")
    }
    server {
      server()
      setConfigName("Fabric Server")
      ideConfigGenerated(true)
      runDir("run/client")
    }
  }
}

publisher {
  setArtifact(remapJar)
}
